
call themis#option('recursive', 1)

let s:root = getcwd()

function! HelpeekTestBeforeEach(assert) abort
    execute 'cd' s:root

    call helpeek#logger#set_func({ msg -> themis#log(msg) })

    function! a:assert.window_count(count) abort
        let window_count = tabpagewinnr(tabpagenr(), '$')
        let message = printf('window count must be %s, but actual: %s', a:count, window_count)
        call self.equals(window_count, a:count, message)
    endfunction

    function! a:assert.contains_line(needle, line) abort
        let line = getline(a:line)
        call self.true(count(line, a:needle) != 0, a:needle . ' must be in the line, but actual: ' . line)
    endfunction

    function! a:assert.not_exists_autocmd(name) abort
        let autocmds = s:redir('autocmd')
        for line in split(autocmds, "\n")
            call self.true(count(line, a:name, v:true) == 0, a:name . ' should not be in the autocmd: ' . line)
        endfor
    endfunction

    filetype on
    syntax enable
endfunction

function! HelpeekTestAfterEach() abort
    tabedit
    tabonly!
    silent! %bwipeout!

    filetype off
    syntax off
endfunction

function! s:redir(cmd) abort
    let [tmp_verbose, tmp_verbosefile] = [&verbose, &verbosefile]
    set verbose=0 verbosefile=
    redir => result
    silent! execute a:cmd
    redir END
    let [&verbose, &verbosefile] = [tmp_verbose, tmp_verbosefile]
    return result
endfunction
